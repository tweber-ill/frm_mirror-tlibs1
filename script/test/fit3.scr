gauss_model(x, x0, sigma, amp, offs)
{
	return amp * exp(-0.5 * ((x-x0) / sigma)^2.) + offs;
}

main()
{
	x = linspace(0, 10, 32);
	y = gauss_model(x, 7., 1., 5., 1.234);
	for(val : y)
		val = val + rand_real(-0.5, 0.5);
	yerr = y/5.;

	hints = [4.5, 1.1, 5.2, 0.];
	hints_err = [0.4, 0.4, 0.4, 2.];

	params = fit("gauss_model", x, y, yerr, ["hints":hints, "hints_errors":hints_err, "debug":1]);
	print("fit parameters: " + str(params));

	params_vals = map_vec_to_val(params);
	params_errs = map_vec_to_val(params, 1);
	print("values: " + str(params_vals));
	print("errors: " + str(params_errs));

	fitx = linspace(min(x), max(x), 128);
	fity = gauss_model(fitx, *params_vals);
	plot([x,y,yerr], [fitx, fity, ["style":"line", "term":"x11"]]);
}
