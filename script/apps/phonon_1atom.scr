#!./hermelin


disp(beta, m, q, a)
{
	# see Furrer p. 90
	om_sq = 4.*beta/m * sin(q*a/2.)^2.;
	return sqrt(om_sq);
}

speedofsound(beta, m, a)
{
	return sqrt(beta/m) * a;
}

usage(app)
{
        print("\nPhonon branch calculation\n\nUsage: e.g. " + app + " m=1 a=1 beta=1");
        print("\tm\tmass of atoms");
        print("\ta\tdistance between two atoms");
        print("\tbeta\tforce constant");
	print();
}

main(args)
{
	beta = 1.;
	m = 1.;
	a = 1.;

	if(length(args) greater 1)
        for(arg : args[1:length(args)])
        {
                if(trim(arg) eq "--help")
                {
			usage(args[0]);
                        return 0;
                }

                arg_split = trim(split(arg, "="));
		register_var(arg_split[0], real(arg_split[1]));
	}


	q = linspace(-pi/a, pi/a, 256);
	om = disp(beta, m, q, a);
	v = speedofsound(beta, m, a);

	xlim = str(min(q)) + " " + str(max(q));

	plopt = ["style":"line", "legend":"phonon dispersion"];

	title = "m=" + m + ", a=" + a + ", beta=" + beta + ", v=" + v;
	plopt2 = ["xlabel":"q", "ylabel":"omega", "title":title, "xlimits":xlim];

	plot([q, om, plopt], plopt2);
}
